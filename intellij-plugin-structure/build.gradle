version = '1.5'

allprojects {
  apply plugin: 'java'

  group = 'org.jetbrains.intellij.plugins'

  sourceCompatibility = 1.6
  targetCompatibility = 1.6
}

//build .jar with sources
task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

//build .jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

repositories {
  maven { url "http://repo.maven.apache.org/maven2" }
}

task prepareTests(type: Copy) {
  def path = "$buildDir/mocks"
  doFirst { println("Copying mock plugins .jar-s to the ${path}") }
  into path
  subprojects {
    from tasks.withType(Jar)
  }
}

test.dependsOn prepareTests


dependencies {
  //mock plugins are used in tests
  testRuntime project(":mock-plugin1")

  compile group: 'org.jdom', name: 'jdom', version: '1.1.3'
  compile group: 'com.google.guava', name: 'guava', version: '18.0'
  compile group: 'commons-io', name: 'commons-io', version: '2.3'
  compile group: 'com.intellij', name: 'annotations', version: '12.0'
  compile group: 'org.ow2.asm', name: 'asm-all', version: '5.0.4'
  compile group: 'org.jsoup', name: 'jsoup', version: '1.8.3'
  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.codehaus.plexus', name: 'plexus-archiver', version: '2.3'
}
