repositories {
  maven { url "http://www.jetbrains.com/intellij-repository/releases" }
}

configurations {
  jpsDependency
}

ext.jpsVersion = '173.3531.6'
ext.jpsPath = file("$buildDir/download/jps-standalone")

FileTree jpsJarsTree = fileTree(dir: "$jpsPath")
jpsJarsTree.include 'jps-model.jar', 'util.jar', 'log4j.jar', 'trove4j.jar', 'jdom.jar'

dependencies {
  compile project(':structure-ide')
  compile project(':structure-classes')
  jpsDependency group: 'com.jetbrains.intellij.idea', name: 'jps-standalone', version: jpsVersion, ext: 'zip'
  compile jpsJarsTree
}

task extractJpsStandalone(type: Copy) {
  def jpsZip = { configurations.jpsDependency.files.first() }
  def outputDir = jpsPath
  from zipTree(jpsZip)
  into outputDir
}

compileJava.dependsOn extractJpsStandalone
compileKotlin.dependsOn extractJpsStandalone

//We have to include IDEA class files and dependencies (trove, jdom, log4j) into structure-ide jar because
//it isn't possible to specify gradle-dependency on these modules: the jps-standalone.zip is published with
//bundled libraries which might have been patched by IDEA team.

//Note: we don't relocate the packages (for example, via shadow-jar plugin ) of these popular dependencies.
//In case of any problems encountered by downstream clients we can bundle the classes into the jar with renaming pre-process.
//The only client for now who uses structure-ide is plugin verifier.
jar {
  from {
    jpsJarsTree.collect { it.isDirectory() ? it : zipTree(it) }
  }
}