buildscript {
  ext {
    grailsVersion = project.grailsVersion
    kotlin_version = '1.0.3'
  }
  repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    classpath "org.springframework:springloaded:1.2.6.RELEASE"
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.5.0"
    classpath "org.grails.plugins:hibernate4:5.0.2"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def verifierVersion = '1.33'

configure(allprojects) {
  version "1.0"
  group "org.jetbrains.intellij.plugins.verifier"

  apply plugin: 'kotlin'
  apply plugin: 'idea'
  apply plugin: 'java'

  idea {
    module {
      inheritOutputDirs = false
      outputDir = file("$buildDir/classes/main")
    }
  }


  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://dl.bintray.com/jetbrains/intellij-plugin-service" }
  }

  dependencies {
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile 'com.github.salomonbrys.kotson:kotson:2.2.2'
    compile group: 'org.jetbrains.intellij.plugins', name: 'verifier-problems', version: verifierVersion
    compile group: 'org.jetbrains.intellij.plugins', name: 'verifier-cli', version: verifierVersion
  }

}

apply plugin: 'maven-publish'

publishing {
  publications {

    def configurePublication = { MavenPublication pub, String projectName, boolean appendFatJar ->

      def proj = project(projectName)
      pub.groupId proj.group
      pub.artifactId proj.name
      pub.version proj.version

      pub.from proj.components.java
      pub.artifact proj.sourcesJar

      if (appendFatJar) {
        pub.artifact proj.shadowJar
      }

    }

    ClientPublication(MavenPublication) { configurePublication(it, ':rest-client', true) }
    ApiPublication(MavenPublication) { configurePublication(it, ':protocol', false) }

  }
}

publishToMavenLocal.dependsOn test
